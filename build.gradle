plugins {
    id 'java-library'
    id 'maven-publish'
    id 'dev.poolside.gradle.semantic-version' version '1.0.0'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'dev.poolside.gradle.semantic-version'

    repositories {
        mavenCentral()
    }

    ext {
        // third party
        jakartaservletapiVersion = "6.0.0"
        jakartawsrsapiVersion = "3.1.0"
        javapoetVersion = "1.13.0"
        javaxservletapiVersion = "4.0.1"
        javaxwsrsapiVersion = "2.1.1"
        protobufVersion = "3.25.5"
        slf4jVersion = "2.0.17"
        springVersion = "6.2.10"
        undertowVersion = "2.3.18.Final"

        // testing
        approvaltestsVersion = "24.22.0"
        javaparserVersion = "3.27.0"
        jerseyCommonJavaxVersion = "2.47"
        jerseyCommonJakartaVersion = "3.1.10"
        junitJupiterVersion = "5.12.2"
        mockitoVersion = "5.18.0"

        // force version for security
        // remove when upgrading other libraries
        commonsLang3Version = "3.18.0" // caused by approvaltestsVersion
    }

    java {
        group = "com.flit"
        targetCompatibility = JavaVersion.VERSION_17
        sourceCompatibility = JavaVersion.VERSION_17
        withSourcesJar()
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    dependencies {
        testImplementation(platform("org.junit:junit-bom:$junitJupiterVersion"))
        testImplementation('org.junit.jupiter:junit-jupiter')
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/github/flit")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
