plugins {
    id 'java-library'
    id 'maven-publish'
    id 'dev.poolside.gradle.semantic-version' version '0.1.5'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
    }

    ext {
        // third party
        jakartaservletapiVersion = "5.0.0"
        jakartawsrsapiVersion = "3.0.0"
        javapoetVersion = "1.13.0"
        javaxservletapiVersion = "4.0.1"
        javaxwsrsapiVersion = "2.1.1"
        protobufVersion = "3.22.2"
        slf4jVersion = "1.7.36"
        springVersion = "5.3.26"
        undertowVersion = "2.3.5.Final"

        // testing
        approvaltestsVersion = "18.5.0"
        javaparserVersion = "3.25.2"
        jerseyCommonJavaxVersion = "2.22.2"
        jerseyCommonJakartaVersion = "3.1.1"
        junitJupiterVersion = "5.9.2"
        mockitoVersion = "5.2.0"
    }

    java {
        group = "com.flit"
        targetCompatibility = JavaVersion.VERSION_11
        sourceCompatibility = JavaVersion.VERSION_11
        withSourcesJar()
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    dependencies {
        testImplementation(platform("org.junit:junit-bom:$junitJupiterVersion"))
        testImplementation('org.junit.jupiter:junit-jupiter')
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/github/flit")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
