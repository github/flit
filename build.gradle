plugins {
    id 'java-library'
    id 'maven-publish'
    id 'dev.poolside.gradle.semantic-version' version '1.0.0'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'dev.poolside.gradle.semantic-version'

    repositories {
        mavenCentral()
    }

    ext {
        // third party
        jakartaservletapiVersion = "6.0.0"
        jakartawsrsapiVersion = "3.1.0"
        javapoetVersion = "1.13.0"
        javaxservletapiVersion = "4.0.1"
        javaxwsrsapiVersion = "2.1.1"
        protobufVersion = "3.23.4"
        slf4jVersion = "1.7.36"
        springVersion = "6.0.11"
        undertowVersion = "2.3.5.Final"

        // testing
        approvaltestsVersion = "18.7.1"
        javaparserVersion = "3.25.4"
        jerseyCommonJavaxVersion = "2.22.2"
        jerseyCommonJakartaVersion = "3.1.3"
        junitJupiterVersion = "5.10.0"
        mockitoVersion = "5.4.0"
    }

    java {
        group = "com.flit"
        targetCompatibility = JavaVersion.VERSION_17
        sourceCompatibility = JavaVersion.VERSION_17
        withSourcesJar()
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    dependencies {
        testImplementation(platform("org.junit:junit-bom:$junitJupiterVersion"))
        testImplementation('org.junit.jupiter:junit-jupiter')
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/github/flit")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
